// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM64.hdl

/**
 * Memory of 64 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    /*
    Dinner time, so some implementation notes:

    - Made up of 8 banks of RAM8 chips.
    - address[0..2] selects which bank.
    - address[3..5] is routed to each RAM8 to select which register.
    - Otherwise just like a RAM8.
    */

    /* Send the load input to the selected address */
    DMux8Way(
      in=load,
      sel=address[0..2],
      a=loadOne,
      b=loadTwo,
      c=loadThree,
      d=loadFour,
      e=loadFive,
      f=loadSix,
      g=loadSeven,
      h=loadEight
    );

    /* Send the selected address to the output */
    Mux8Way16(
      a=one,
      b=two,
      c=three,
      d=four,
      e=five,
      f=six,
      g=seven,
      h=eight,
      sel=address[0..2],
      out=out
    );

    /* Memory registers */
    RAM8(in=in, load=loadOne, address=address[3..5], out=one);
    RAM8(in=in, load=loadTwo, address=address[3..5], out=two);
    RAM8(in=in, load=loadThree, address=address[3..5], out=three);
    RAM8(in=in, load=loadFour, address=address[3..5], out=four);
    RAM8(in=in, load=loadFive, address=address[3..5], out=five);
    RAM8(in=in, load=loadSix, address=address[3..5], out=six);
    RAM8(in=in, load=loadSeven, address=address[3..5], out=seven);
    RAM8(in=in, load=loadEight, address=address[3..5], out=eight);
}
